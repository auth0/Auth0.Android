/*
 * build.gradle
 *
 * Copyright (c) 2016 Auth0 (http://auth0.com)
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in
 * all copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN
 * THE SOFTWARE.
 */

plugins {
    id 'com.android.library'
    id 'kotlin-android'
}

apply from: rootProject.file('gradle/versioning.gradle')

version = getVersionFromFile()

logger.lifecycle("Using version ${version} for ${name}")

android {
    namespace 'com.auth0.android.auth0'
    compileSdk 36

    buildFeatures {
        buildConfig = true
    }

    defaultConfig {
        minSdkVersion 21
        targetSdk 36
        versionCode 1
        versionName project.version

        buildConfigField "String", "LIBRARY_NAME", "\"$project.rootProject.name\""
        buildConfigField "String", "VERSION_NAME", "\"${project.version}\""

        manifestPlaceholders = [auth0Domain: '${auth0Domain}', auth0Scheme: '${auth0Scheme}']

        consumerProguardFiles '../proguard/proguard-gson.pro', '../proguard/proguard-okio.pro'
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    lint {
        htmlReport = true
        abortOnError = true
    }
    testOptions {
        unitTests {
            all {
                maxHeapSize = "1024m"
            }
            includeAndroidResources = true
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_17.toString()
        freeCompilerArgs += [
                '-Xexplicit-api=strict', // or '-Xexplicit-api=warning'
        ]
    }
}

ext {
    okhttpVersion = '5.0.0'
    coroutinesVersion = '1.10.2'
    biometricLibraryVersion = '1.1.0'
}


dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.15.0'
    implementation 'androidx.appcompat:appcompat:1.7.0'
    implementation 'androidx.browser:browser:1.9.0'
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutinesVersion"
    implementation "com.squareup.okhttp3:okhttp:$okhttpVersion"

    implementation "com.squareup.okhttp3:logging-interceptor:$okhttpVersion"
    implementation 'com.google.code.gson:gson:2.11.0'
    implementation 'com.google.androidbrowserhelper:androidbrowserhelper:2.5.0'

    implementation "androidx.biometric:biometric:$biometricLibraryVersion"

    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.hamcrest:java-hamcrest:2.0.0.0'
    testImplementation 'org.mockito:mockito-core:5.14.0'
    testImplementation 'org.mockito.kotlin:mockito-kotlin:5.4.0'
    testImplementation "com.squareup.okhttp3:mockwebserver3:$okhttpVersion"
    testImplementation "com.squareup.okhttp3:okhttp-tls:$okhttpVersion"
    testImplementation 'com.jayway.awaitility:awaitility:1.7.0'
    testImplementation 'org.robolectric:robolectric:4.15.1'
    testImplementation 'androidx.test.espresso:espresso-intents:3.6.1'
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:$coroutinesVersion"

    testImplementation "androidx.biometric:biometric:$biometricLibraryVersion"
}

apply from: rootProject.file('gradle/jacoco.gradle')
apply from: rootProject.file('gradle/maven-publish.gradle')